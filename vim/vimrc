" Vimrc

" Pathogen plugin setup {{{1
" =====================

execute pathogen#infect()

" VIM global options {{{1
" ==================

" Don't timeout key sequences
set notimeout

" Timeout key codes (don't wait after Esc)
set ttimeout
set timeoutlen=10

" No vim compatibility
set nocompatible

" No bell sounds
set noerrorbells
set visualbell
set vb t_vb=""

" Enable xterm mouse support in all modes
set mouse=a

" Disable modeline for security reasons
set nomodeline

" Syntax options {{{1
" =======================

" Set syntax highlighting
syntax on

" Limit highlighting to x columns to increase speed with long lines
set synmaxcol=2048

" History and swap options {{{1
" ========================

"Set maximum undo levels
set undolevels=1000

" Swap files location
set directory=~/tmp/swap/
" Backup files directory
set backupdir=~/tmp/backup/

if has("persistent_undo")
    " Undo files directory
    set undodir=~/tmp/undo/
    " Enable undo file for persistent undo
    set undofile
    " Maximum number of lines to save for undo on buffer reload
    set undoreload=10000
endif

" Enable backups
set backup

" Store netrw file in ~/tmp/
let g:netrw_home= expand("$HOME/tmp")

" File options {{{1
" ============

" Set default file encoding to UTF-8
set encoding=utf-8

" Use UNIX (\n) line endings for new files
set fileformat=unix

" Enable filetype plugin
filetype plugin on

" Indent depending on filetype
filetype indent on

" Jump to the last position when reopening a file
if has("autocmd")
  au BufReadPost * if line("'\"") > 1 && line("'\"") <= line("$") | exe "normal! g'\"" | endif
endif

" Viewport behavior options (buffers and windows) {{{1
" ===============================================

" Do not warn when switching from unsaved buffer
set hidden

" Autoreload file when changed outside of vim
set autoread

" Create new split window below the current one
set splitbelow

" Create vertical split window right of the current one
set splitright

" Search and Movement Options {{{1
" ==============

" Incremental search
set incsearch

" Highlight search results
set hlsearch

" Case-insensitive search
set ignorecase

" Case sensitive if search string contains uppercase letters
set smartcase

" Don't move on *
nnoremap <silent> * :let stay_star_view = winsaveview()<cr>*<c-o>:call winrestview(stay_star_view)<cr>

" Use Emacs C-A and C-E behavior in insert and command mode
inoremap <C-a> <Esc>I
inoremap <C-e> <Esc>A
cnoremap <C-a> <Home>
cnoremap <C-e> <End>

" Don't skip wrap lines
nnoremap j gj
nnoremap k gk

" Show ↪ at the beginning of wrapped lines
let &showbreak = nr2char(8618)

" Formatting options {{{1
" ==================

" Configure autowrapping and auto comment leader insertion
set formatoptions=ctrqln

" Insert only one space when joining lines at sentence end
set nojoinspaces

" General view options {{{1
" ====================

" Show line numbers
set nu

" Highlight current line
set cursorline

" Highlight column
" set colorcolumn=85

" Set interface elements symbols
set fillchars=vert:│

" Show invisible symbols
set list
set listchars=tab:▸\ ",eol:¬

" Show current normal mode command
set showcmd

" Don't display line numbers
set nonumber

" Status line options {{{1
" ===================

" Always show statusline
set laststatus=2

" Status line
if has('statusline')
    set statusline=%<%f\  " Current file path
    set statusline+=%m%h%r%w " File flags - modified, help buffer, readonly, preview
    set statusline+=%y " Filetype
    set statusline+=%= " Indent to the right
    set statusline+=%-20.(%4.l/%L,\ %c%V\ [x%B]%)\  "Current line number/total, column number, byte code
    set statusline+=%P\  "Percentage through a file
    "set statusline+=[%{&fileformat}] " file format (unix/mac/win)
    set statusline+=[%{(&fenc==\"\"?&enc:&fenc)}]\  "File encoding
    set statusline+={%n} "Buffer number
endif

" Completion options {{{1
" ==================

" Enable menu at the bottom of vim window
set wildmenu
" Set menu behavior
set wildmode=list:longest,full

" Set competion menu behavior, preview window disabled
" Complete only common letters among possible completions, not the while first
" option
set completeopt=menu,longest ",preview

" Autohide preview window after selection
au CursorMovedI,InsertLeave * if pumvisible() == 0|silent! pclose|endif

" Racket is scheme, and we have only syntax completion for scheme
au BufRead,BufNewFile *.rkt set filetype=scheme

" When doing tab completion, give the following files lower priority
set suffixes+=.info,.aux,.log,.dvi,.bbl,.out,.o,.loi,.pyc

" Exclude files from listing
set wildignore+=*.aux,*.out,*.toc " LaTeX intermediate files
set wildignore+=*.jpg,*.bmp,*.gif,*.png,*.jpeg,*.ico " binary images
set wildignore+=*.pyc,*.luac,*.beam " byte code
set wildignore+=*.o,*.obj,*.manifest " compiled object files
set wildignore+=.DS_Store " system files

" Using chicken scheme
let g:is_chicken = 1

" Indentation and tab options {{{1
" ===========================

" Always convert tabs to spaces
set expandtab

" Use shiftwidth value for softtabstop
set smarttab

" What to use for an >> and << indent.
set shiftwidth=4

" Round indent to multiple of shiftwidth
set shiftround

" Keep indentation level from previous line
set autoindent

" Folding options {{{1
" ===============

" Folding based on indentation:
set foldmethod=indent

" Open all folds in new buffer
au BufRead * normal zR

" General keymappings {{{1
" ===================

" Set map leaders
let mapleader = ","
let maplocalleader = "\\"

" Reset highlighting
nmap <leader><Space> :nohlsearch<CR>

" Jump to previous buffer with Q
noremap <silent>Q <C-^>

" Jump between opening/closing pair with Tab
noremap <Tab> %

" Since <Tab> == <C-i>, bind jumplist forward to <C-n>
nnoremap <C-n> <C-i>

" Make Y behave like others do
nnoremap Y y$

" Toggle folds with Space
nnoremap <Space> za
vnoremap <Space> za

" Switch fold method with ,zs and ,zm
noremap <leader>zs :set foldmethod=syntax<CR>
noremap <leader>zm :set foldmethod=marker<CR>
noremap <leader>zi :set foldmethod=indent<CR>

" Yank to system clipboard
noremap <leader>y "*y

" Paste from system clipboard
noremap <leader>p :set paste<CR>"*p<CR>:set nopaste<CR>
noremap <leader>P :set paste<CR>"*P<CR>:set nopaste<CR>

" Use <M-CR> to continue current line
imap <M-CR> \<CR>

" Use <C-q> in GUI and <C-c> in shell as prefix for personal commands
nmap <C-q> <C-c>
vmap <C-q> <C-c>

" Set backspace behavior
set backspace=indent,eol,start

" Clean whitespace
map <C-c>w mw:%s/\s\+$//<cr>:let @/=''<CR>`w

" Call par reformater on paragraph with textwidth 100
map <C-c>f {v}!par w100<CR>

" Call par reformater on selection with textwidth 100
vmap <C-c>f !par w100<CR>

nnoremap <C-c>t :silent !/usr/local/bin/ctags -R .<CR><C-L>

" Show the name of highlightning group under cursor - usefull for syntax theme
" editing
nmap <silent> <C-F10> :echo "hi<" . synIDattr(synID(line("."),col("."),1),"name")
     \ . '> trans<' . synIDattr(synID(line("."),col("."),0),"name")
     \ . "> lo<" . synIDattr(synIDtrans(synID(line("."),col("."),1)),"name")
     \ . ">"<CR>h

" Disable F1
nnoremap <F1> <nop>

" Navigate lists with arrows
nnoremap <left>  :cprev<cr>zvzz
nnoremap <right> :cnext<cr>zvzz
nnoremap <up>    :lprev<cr>zvzz
nnoremap <down>  :lnext<cr>zvzz

" Helper commands {{{1
" ===============

" Sudo to save file
command! Sudow w !sudo tee % >/dev/null

" Color and gui options {{{1
" =====================

" Set gui-specific options
if has("gui_running")
    " Set color scheme
    colorscheme prefect
    " Disable toolbar
    set guioptions-=T
    " Disable left scrollbar
    set guioptions-=l
    set guioptions-=L
    " Disable right scrollbar
    set guioptions-=r
    set guioptions-=R
    " Disable bottom scrollbar
    set guioptions-=b
    " Set gui window size
    set lines=80 columns=100
else
    " Set color scheme
    let g:jellybeans_background_color_256 = "232"
    let g:jellybeans_overrides = {"MatchParen": {'256ctermbg': '63'}}
    colorscheme jellybeans
endif

" Stretch window in fullscreen mode
if has("gui_macvim")
   set fuoptions=maxvert,maxhorz
endif

" Default statusline color when entering Vim
hi statusline ctermbg=253 guibg=#e0e0e0

" Change status line color in insert mode
if has("autocmd")
  au InsertEnter * hi StatusLine ctermbg=38 guibg=#6ac3d8
  au InsertLeave * hi StatusLine ctermbg=253 guibg=#e0e0e0
endif

" Specific highlight options {{{1
" ==========================

" Highlight group when displaying bad whitespace is desired.
highlight BadWhitespace ctermbg=red guibg=red

fun! s:MatchBadWhitespace(current)
    if exists('b:hide_bad_whitespace')
        match none
        return
    endif
    if a:current
        match BadWhitespace /\s\+$/
    else
        match BadWhitespace /\s\+\%#\@<!$/
    endif
endfun

augroup WhitespaceMatch
    " Highlight whitespace on open buffer
    autocmd BufWinEnter * call s:MatchBadWhitespace(1)
    " Don't highlight while typing
    autocmd InsertEnter * call s:MatchBadWhitespace(0)
    autocmd InsertLeave * call s:MatchBadWhitespace(1)
    " Clear match when leaving buffer to avoid memory leaks
    autocmd BufWinLeave * call clearmatches()
augroup END

" Netrw options {{{1
" =============

" Disable netrw banner
let g:netrw_banner=0

" Disable mouse maps
let g:netrw_mousemaps=0

" Plugin-specific settings {{{1
" ========================

" Pylint {{{2
" ------

" Disable onwrite pylint checks
let g:pylint_onwrite = 0

" TagBar {{{2
" ------
" Bind show/hide tag list
nmap <silent><leader>t :TagbarToggle<CR>

" Additional filetype support
let g:tagbar_type_markdown = {
    \ 'ctagstype' : 'markdown',
    \ 'kinds' : ['h:headings'],
    \ 'sort' : 0}

" TaskList {{{2
" --------
" Bind ,T to show tasklist
nmap <silent><leader>T <Plug>TaskList


" Show Tagbar window on the left
let g:tagbar_left = 1

" Set tagbar window width
let g:tagbar_width = 30

" Change focus to Tagbar on open
let g:tagbar_autofocus = 1

" Don't sort tags by name
let g:tagbar_sort = 0

" Don't show help line
let g:tagbar_compact = 1

" CtrlP {{{2
" ---------

" ,e to search for file name
let g:ctrlp_map = '<leader>e'

" ,q to search for buffer name
nmap <silent><leader>q :CtrlPBuffer<CR>

" ,/ to search for tag in all open buffers
nmap <silent><leader>/ :CtrlPBufTagAll<CR>

" Don't change the working directory
let g:ctrlp_working_path_mode = 0

" Disable most recently used files list
let g:ctrlp_mru_files = 0

" Don't seach for dotfiles/dirs
let g:ctrlp_dotfiles = 0

" Open new files in the current window
let g:ctrlp_open_new_file = 0

" Add paths to CtrlP ignore
let g:ctrlp_custom_ignore = 'node_modules\|.git'

" Add more filetypes to CtrlPBufTagAll
let g:ctrlp_buftag_types = {
    \ 'clojure': '',
    \ 'markdown': '',
    \ }


" SuperTab {{{2
" --------

" Set supertab to complete depending on text before cursor
let g:SuperTabDefaultCompletionType="context"

" Use custom context function from autoload/completion
let g:SuperTabCompletionContexts = ['completion#ExtendedContextText']

" Set backwards mapping to work with snipmate's reverse tabstops
let g:SuperTabMappingBackward ='<C-Tab>'

" Disable cr to fix conflict with delimitmate
let g:SuperTabCrMapping = '<C-CR>'

" Syntastic {{{2
" ---------

" Enable syntax error signs, disable prewiew window with error list
let g:syntastic_enable_signs=1
let g:syntastic_auto_loc_list=0

function! g:ToggleErrors()
    redir =>buflist
    silent! ls
    redir END
    for bufnum in filter(split(buflist, '\n'), 'v:val =~ "Location List"')
        lclose
        return
    endfor
    Errors
endfunction


" Bind :Errors to hotkey
nmap <silent> <leader>E :call g:ToggleErrors()<CR>

" Ignore line-too-long errors with flake8
let g:syntastic_python_flake8_args = '--ignore=E501'

" Gundo {{{2
" -----
nmap <silent><leader>u :GundoToggle<CR>

" BufExplorer {{{2
" -----------

" Map to ,b
nmap <Plug>BufExplorerVerticalSplit :BufExplorerVerticalSplit
nmap <Plug>BufExplorerHorizontalSplit :BufExplorerHorizontalSplit
nnoremap <silent><leader>b :BufExplorer<CR>

" Disable help string
let g:bufExplorerDefaultHelp=0

" Ropevim {{{2
" -------

" Change prefixes to <C-c>
let g:ropevim_local_prefix = "<C-c>r"
let g:ropevim_global_prefix = "<C-c>p"

" Disable shorcuts
let g:ropevim_enable_shortcuts = 0

" Python syntax {{{2
" -------------

" Python full highlighting
let g:python_highlight_all = 1
let g:python_version_2 = 1

" Highlight print as keyword
let g:python_print_as_function = 0


" VimErl {{{2
" ------
let g:erlangCompletionDisplayDoc = 0

" UltiSnips {{{2
" ---------

" Set jump forward key
let g:UltiSnipsJumpForwardTrigger = "<Tab>"

" Set jump backward key
let g:UltiSnipsJumpBackwardTrigger = "<S-Tab>"

" List all available snippets
let g:UltiSnipsListSnippets = "<F1>"

" Set UltiSnips edit snippet directory
let g:UltiSnipsSnippetsDir = '~/.vim/usnippets'

" Set UltiSnips folder names
let g:UltiSnipsSnippetDirectories = ["UltiSnips", "usnippets"]

" Use hardtabs in snippets for agile sw handling
au FileType snippets setl noexpandtab

" Vimwiki {{{2
" -------

" Default wiki with .md files and vimwiki_markdown syntax file
let g:vimwiki_list = [{'path': '~/wiki', 'ext': '.md', 'syntax':'markdown'}]

" Use markdown filetype for files outside wiki path
let g:vimwiki_global_ext = 0

" Highlight headers
let g:vimwiki_hl_headers = 1

" Use Mac OS X `open` for external links
let g:vimwiki_browsers = ['open']

" Enable ctags markdown headers listing
let g:tlist_vimwiki_settings = 'markdown;h:Headers'

" Set up folding
let g:vimwiki_folding = 1
let g:vimwiki_fold_lists = 1

" Disable Tab mapping for table formatting
let g:vimwiki_table_auto_fmt = 0

" Disable wikiwords
let g:vimwiki_camel_case = 0

" Slime {{{2
" -----

" Use tmux instead of screen
let g:slime_target = "tmux"

" Use IPython cpaste
let g:slime_python_ipython = 1

" Smartinput {{{2
" ----------

" Expand [] and () with Enter
call smartinput#define_rule({'at': '(\%#)', 'char': '<Enter>', 'input': '<Enter>X<Enter><Up><End><C-t><BS>'})
call smartinput#define_rule({'at': '\[\%#\]', 'char': '<Enter>', 'input': '<Enter>X<Enter><Up><End><C-t><BS>'})

" Don't autoclose in front of a word or quote
call smartinput#define_rule({'at': '\%#[-a-zA-Z0-9_.,=+*\''"([{]', 'char': '(', 'input': '('})
call smartinput#define_rule({'at': '\%#[-a-zA-Z0-9_.,=+*\''"([{]', 'char': '[', 'input': '['})
call smartinput#define_rule({'at': '\%#[-a-zA-Z0-9_.,=+*\''"([{]', 'char': '{', 'input': '{'})
call smartinput#define_rule({'at': '\%#[-a-zA-Z0-9_.,=+*\"]', 'char': '''', 'input': ''''})
call smartinput#define_rule({'at': '\%#[-a-zA-Z0-9_.,=+*\'']', 'char': '"', 'input': '"'})

" Don't ever autoclose `
call smartinput#define_rule({'at': '\%#', 'char': '`', 'input': '`'})

" YankStack {{{2
" --------

" Initialize yankstack mappings before surround
call yankstack#setup()

" Cycle through yank history with <C-p>
nmap <C-p> <Plug>yankstack_substitute_older_paste

" Dispatch {{{2
" --------

" Run async command with ,d
nmap <leader>d :Dispatch!

" Vim-sexp {{{2
" -------

" Disable insert mode maps
let g:sexp_enable_insert_mode_mappings = 0

" Don't enter insert mode
let g:sexp_insert_after_wrap = 0

" Emmet {{{2
" -----

" Use <C-c>e for expansion
let g:user_emmet_expandabbr_key = "<C-c>e"
